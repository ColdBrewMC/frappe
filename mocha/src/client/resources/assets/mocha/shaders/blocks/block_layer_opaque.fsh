#version 330 core

#import <sodium:include/fog.glsl>
#import <sodium:include/chunk_material.glsl>

// Duct tape
#define moj_import import

// Standard FRP uniforms

// Experimental FRP uniforms
#define frp_exp_GlintAlpha u_FrappeCompatGlintAlpha
#define frp_exp_FogColor u_FogColor
#define frp_exp_LevelTime u_FrappeCompatLevelTime
#define frp_exp_RGSSEnabled u_UseRGSS
#define frp_exp_BlockAtlasTexture u_BlockTex
#define frp_exp_AtlasTextureSize ivec2(int(u_FrappeCompatTextureSize.x), int(u_FrappeCompatTextureSize.y))

// Standard FRP vertex data
#define v_frp_MaterialID v_FrappeMaterialId

// Experimental FRP vertex data
#define v_frp_exp_ChunkFade fadeFactor
#define v_frp_exp_UV v_FrappeUV

in vec4 v_Color; // The interpolated vertex color
in vec2 v_TexCoord; // The interpolated block texture coordinates
in vec2 v_FragDistance; // The fragment's distance from the camera (cylindrical and spherical)
in float fadeFactor;
#ifdef _FRAPPE_COMPLEX_MATERIAL
in vec2 v_FrappeUV;
#endif

flat in uint v_Material;
flat in uint v_FrappeMaterialId;

uniform sampler2D u_BlockTex; // The block texture

uniform vec4 u_FogColor; // The color of the shader fog
uniform vec2 u_EnvironmentFog; // The start and end position for environmental fog
uniform vec2 u_RenderFog; // The start and end position for border fog
uniform vec2 u_TexelSize;
uniform bool u_UseRGSS;
uniform vec2 u_FrappeCompatTextureSize;
uniform float u_FrappeCompatGlintAlpha;
uniform float u_FrappeCompatLevelTime;

uniform int u_CurrentTime;

out vec4 fragColor; // The output fragment for the color framebuffer

vec4 sampleNearest(sampler2D sampler, vec2 uv, vec2 pixelSize, vec2 du, vec2 dv, vec2 texelScreenSize) {
	// Convert our UV back up to texel coordinates and find out how far over we are from the center of each pixel
	vec2 uvTexelCoords = uv / pixelSize;
	vec2 texelCenter = round(uvTexelCoords) - 0.5f;
	vec2 texelOffset = uvTexelCoords - texelCenter;

	// Move our offset closer to the texel center based on texel size on screen
	texelOffset = (texelOffset - 0.5f) * pixelSize / texelScreenSize + 0.5f;
	texelOffset = clamp(texelOffset, 0.0f, 1.0f);

	uv = (texelCenter + texelOffset) * pixelSize;
	return textureGrad(sampler, uv, du, dv);
}

vec4 sampleNearest(sampler2D source, vec2 uv, vec2 pixelSize) {
	vec2 du = dFdx(uv);
	vec2 dv = dFdy(uv);
	vec2 texelScreenSize = sqrt(du * du + dv * dv);
	return sampleNearest(source, uv, pixelSize, du, dv, texelScreenSize);
}

// Rotated Grid Super-Sampling
vec4 sampleRGSS(sampler2D source, vec2 uv, vec2 pixelSize) {
	vec2 du = dFdx(uv);
	vec2 dv = dFdy(uv);

	vec2 texelScreenSize = sqrt(du * du + dv * dv);
	float maxTexelSize = max(texelScreenSize.x, texelScreenSize.y);

	float minPixelSize = min(pixelSize.x, pixelSize.y);

	float transitionStart = minPixelSize * 1.0;
	float transitionEnd = minPixelSize * 2.0;
	float blendFactor = smoothstep(transitionStart, transitionEnd, maxTexelSize);

	float duLength = length(du);
	float dvLength = length(dv);
	float minDerivative = min(duLength, dvLength);
	float maxDerivative = max(duLength, dvLength);

	float effectiveDerivative = sqrt(minDerivative * maxDerivative);

	float mipLevelExact = max(0.0, log2(effectiveDerivative / minPixelSize));

	const vec2 offsets[4] = vec2[](
	vec2(0.125, 0.375),
	vec2(-0.125, -0.375),
	vec2(0.375, -0.125),
	vec2(-0.375, 0.125)
	);

	vec4 rgssColor = vec4(0.0);
	for (int i = 0; i < 4; ++i) {
		vec2 sampleUV = uv + offsets[i] * pixelSize;
		rgssColor += textureLod(source, sampleUV, mipLevelExact);
	}
	rgssColor *= 0.25;

	vec4 nearestColor = sampleNearest(source, uv, pixelSize, du, dv, texelScreenSize);

	return mix(nearestColor, rgssColor, blendFactor);
}

#import <mocha:fragment.glsl>

void main() {
	vec4 color = u_UseRGSS ? sampleRGSS(u_BlockTex, v_TexCoord, u_TexelSize) : sampleNearest(u_BlockTex, v_TexCoord, u_TexelSize);
	color *= v_Color; // Apply per-vertex color modulator

	#ifdef _FRAPPE_SIMPLE_MATERIAL
	color = _frp_simple_pre_fragment(color);
	#endif
	#ifdef _FRAPPE_COMPLEX_MATERIAL
	color = _frp_pre_fragment(color);
	#endif

	#ifdef USE_FRAGMENT_DISCARD
	if (color.a < _material_alpha_cutoff(v_Material)) {
		discard;
	}
	#endif

	fragColor = _linearFog(color, v_FragDistance, u_FogColor, u_EnvironmentFog, u_RenderFog, fadeFactor);
}
